import java.nio.file.StandardCopyOption
import java.nio.file.Files
import java.text.SimpleDateFormat
import java.util.Date

// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:java_packaging
plugins {
    id 'java'
    // For source code formatting
    id "com.diffplug.spotless" version "8.1.0"
	// https://github.com/harbby/gradle-serviceloader
    id "com.github.harbby.gradle.serviceloader" version "1.1.9"
	// Shadow
	id "com.gradleup.shadow" version "9.3.1"
    // Download task
    id "de.undercouch.download" version "5.6.0"
	// Task visualizer
	id "com.dorongold.task-tree" version "4.0.1"
	// Maven Publisher
	id 'maven-publish'
	id 'signing'
	id 'com.gradleup.nmcp.aggregation' version "1.4.2"
}

/**
 * Project Properties
 */
ext {
	buildID = System.getenv( 'BUILD_ID' ) ?: '0'
	branch = System.getenv( 'BRANCH' ) ?: 'development'
}

if (branch == 'development') {
    // If the branch is 'development', ensure the version ends with '-snapshot'
    // This replaces any existing prerelease identifier with '-snapshot'
    version = version.contains('-') ? version.replaceAll(/-.*/, '-snapshot') : "${version}-snapshot".toString()
	boxlangVersion = boxlangVersion.contains('-') ? boxlangVersion.replaceAll(/-.*/, '-snapshot') : "${boxlangVersion}-snapshot".toString()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
	// LOCAL DEVELOPMENT ONLY
	// CHOOSE THE RIGHT LOCATION FOR YOUR LOCAL DEPENDENCIES
	if ( file( '../boxlang/build/libs/boxlang-' + boxlangVersion + '.jar' ).exists() ) {
		implementation files( '../boxlang/build/libs/boxlang-' + boxlangVersion + '.jar' )
		implementation files( '../boxlang-web-support/build/libs/boxlang-web-support-' + boxlangVersion + '.jar' )
	} else {
		// Downloaded Dependencies
		implementation files( 'src/test/resources/libs/boxlang-' + boxlangVersion + '.jar' )
		implementation files( 'src/test/resources/libs/boxlang-web-support-' + boxlangVersion + '.jar' )
	}

	// Servlet API
	compileOnly 'jakarta.servlet:jakarta.servlet-api:5.0.0'
	// We only need these for the PageContext class
	compileOnly 'jakarta.servlet.jsp:jakarta.servlet.jsp-api:4.0.0'
	compileOnly 'jakarta.el:jakarta.el-api:5.0.0'
 	implementation 'org.apache.commons:commons-fileupload2-jakarta-servlet5:2.0.0-M4'

    // Testing Dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:6.+"
	testImplementation "org.mockito:mockito-core:5.+"
	testImplementation "com.google.truth:truth:1.+"
	// Explicitly declare the JUnit platform launcher (to avoid deprecation)
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

	// Integration Testing Dependencies (for testServer task)
	testImplementation "org.eclipse.jetty:jetty-server:11.0.26"
	testImplementation "org.eclipse.jetty:jetty-webapp:11.0.26"
	testImplementation "org.eclipse.jetty:jetty-servlet:11.0.26"
	testImplementation "org.apache.httpcomponents.client5:httpclient5:5.6"
	testImplementation "org.apache.httpcomponents.core5:httpcore5:5.3.5"
}

java {
	sourceCompatibility = JavaVersion.toVersion( jdkVersion )
	targetCompatibility = JavaVersion.toVersion( jdkVersion )
	withJavadocJar()
	withSourcesJar()
}

compileJava {
	// Compiler Options
	options.incremental = true
	options.encoding = 'UTF-8'
	options.debug = true
}

compileTestJava {
	source sourceSets.test.java
	dependsOn compileJava, serviceLoaderBuild
	options.encoding = 'UTF-8'
}

jar {
    archiveVersion =  "${version}"
 	manifest {
	   attributes 'Description': 'The BoxLang Servlet Runtime'
	   attributes 'Implementation-Version': "${version}+${buildID}"
	   attributes 'Created-On': new SimpleDateFormat( "yyyy-MM-dd HH:mm:ss" ).format( new Date() )
	   attributes 'Created-By': "Ortus Solutions, Corp"
    }
}

shadowJar {
	archiveClassifier = ""
	destinationDirectory = file( "build/distributions" )

	// Configure duplicates strategy and service file merging for Shadow 9.0+
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	mergeServiceFiles()

	exclude "schema/**"
	exclude "org/antlr/v4/gui/**"
	exclude "org/antlr/v4/gui/**"
	exclude "com/ibm/icu/**"
}

/**
 * Test task to verify that service files are properly merged in the shadow JAR
 */
task testServiceFileMerging( type: Test ) {
	description = 'Validates that the Shadow JAR contains all merged service files from dependencies'
	group = 'verification'

	// Only run the service file merging test
	useJUnitPlatform()
	include '**/ServiceFileMergingTest.class'

	// Make sure the shadowJar is built first
	dependsOn shadowJar

	// Don't run this as part of the normal test task
	// It's specifically for validating the final artifact

	doFirst {
		println "ðŸ” Testing Shadow JAR service file merging..."
	}
}

// Add service file validation to the build process
build.dependsOn( shadowJar, testServiceFileMerging );

/**
 * Publish the artifacts to the local maven repository
 */
publishing {
    publications {
        shadow( MavenPublication ) { publication ->
			artifact shadowJar
			artifact javadocJar
			artifact sourcesJar

			// This is the only one sonatype accepts, not ortus.boxlang
			// https://central.sonatype.com/
			groupId = 'io.boxlang'
			artifactId = 'boxlang-servlet'
			pom {
				name = "BoxLang Servlet Runtime"
				description = "BoxLang is a dynamic multi-runtime JVM Language based on fluency and functional constructs. This is the Servlet Runtime for BoxLang."
				url = "https://boxlang.io"
				issueManagement {
					system = "Jira"
					url = "https://ortussolutions.atlassian.net/jira/software/c/projects/BL/issues"
				}
				mailingLists {
					mailingList {
						name = "BoxLang Community"
						subscribe = "https://community.ortussolutions.com/c/boxlang/42"
						unsubscribe = "https://community.ortussolutions.com/c/boxlang/42"
					}
				}
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/ortus-boxlang/boxlang-servlet.git'
					developerConnection = 'scm:git:ssh://github.com/ortus-boxlang/boxlang-servlet.git'
					url = 'https://github.com/ortus-boxlang/boxlang-servlet'
				}
				developers{
					developer {
						id  = "lmajano"
						name = "Luis Majano"
						email = "lmajano@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
					developer {
						id = "bdw429s"
						name = "Brad Wood"
						email = "brad@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
					developer {
						id = "jclausen"
						name = "Jon Clausen"
						email = "jclausen@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
					developer {
						id = "michaelborn"
						name = "Michael Born"
						email = "michael@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
					developer {
						id = "garciadev"
						name = "Daniel Garcia"
						email = "dgarcia@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
					developer {
						id = "jbeers"
						name = "Jacob Beers"
						email = "jbeers@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
					developer {
						id = "gpickin"
						name = "Gavin Pickin"
						email = "gavin@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
					developer {
						id = "ericpeterson"
						name = "Eric Peterson"
						email = "eric@ortussolutions.com"
						organization = "Ortus Solutions, Corp"
						organizationUrl = "https://www.ortussolutions.com"
					}
				}
			}
        }
    }

    repositories {
        maven {
            name = 'local-repo'
            url = layout.buildDirectory.dir( "repo" )
        }
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/ortus-boxlang/boxlang-servlet"
			credentials {
				username = System.getenv( "GITHUB_ACTOR" )
				password = System.getenv( "GITHUB_TOKEN" )
			}
		}
    }
}

/**
 * Maven Central Publishing
 * Plugin Information here: https://github.com/gradleup/nmcp
 */
nmcpAggregation {
	centralPortal {
		username = System.getenv( "MAVEN_USERNAME" ) ?: findProperty( "maven_username" )
		password = System.getenv( "MAVEN_PASSWORD" ) ?: findProperty( "maven_password" )
		// Manual release from the Portal UI:
		// publishingType = "USER_MANAGED"
		// Or auto-release:
		publishingType = "AUTOMATIC"
	}

	// Publishes all subprojects that apply 'maven-publish'
  	publishAllProjectsProbablyBreakingProjectIsolation()
}

/**
 * Digital Signing of assets using the ortus GPG Key
 */
signing {
	// Check if we have environment variables for in-memory PGP keys (CI/CD)
	def gpgKey = System.getenv( "GPG_KEY" )
	def gpgPassword = System.getenv( "GPG_PASSWORD" )

	if ( gpgKey && gpgPassword ) {
		// Use in-memory PGP keys for CI/CD
		useInMemoryPgpKeys( gpgKey, gpgPassword )
	}
	// Otherwise use traditional gradle.properties approach for local development

    sign publishing.publications.shadow
}

/**
 * Docs are here:
 * - https://github.com/harbby/gradle-serviceloader,
 * - https://plugins.gradle.org/plugin/com.github.harbby.gradle.serviceloader
 * This generates the META-INF/services files for the ServiceLoader as part of the `build` task
 */
serviceLoader {
    serviceInterface 'ortus.boxlang.runtime.bifs.BIF'
    serviceInterface 'ortus.boxlang.runtime.components.Component'
    serviceInterface 'ortus.boxlang.runtime.async.tasks.IScheduler'
    serviceInterface 'ortus.boxlang.runtime.cache.providers.ICacheProvider'
	serviceInterface 'ortus.boxlang.runtime.events.IInterceptor'
}

/**
 * Token Replacements for files
 */
import org.apache.tools.ant.filters.ReplaceTokens
processResources {
	// Replace @build.date@ with the current date in META-INF/version.properties file
	filter( ReplaceTokens, tokens: [ 'build.date': new SimpleDateFormat( "yyyy-MM-dd HH:mm:ss" ).format( new Date() ) ] )
	// Replace @build.version@ with the current version in META-INF/version.properties file
	filter( ReplaceTokens, tokens: [ 'build.version': version + "+" + buildID ] )
}

javadoc {
    // To avoid our own doclet issues
	options.addBooleanOption( "Xdoclint:none", true )
	options.addBooleanOption( 'html5', true )
}
task zipJavadocs( type: Zip ) {
	group "documentation"
	from javadoc.destinationDir
	archiveFileName = "${project.name}-javadocs-${version}.zip"
	destinationDirectory = file( "$buildDir/distributions" )
	// Output that the docs have been zippped
	doLast {
		println "+ Javadocs have been zipped to the distribution folder"
	}
}
javadoc.finalizedBy( zipJavadocs )

/**
 * Source Code Formatting
 */
spotless {
    java {
		target fileTree( "." ) {
            include "**/*.java"
            exclude "**/build/**", "bin/**", "examples/**"
        }
        eclipse().configFile( "./.ortus-java-style.xml" )
		toggleOffOn()
    }
}

test {
    useJUnitPlatform()
    testLogging {
		showStandardStreams = true
	}
	// Exclude integration tests from regular test task
	// Integration tests should only run via the 'testServer' task
	exclude '**/integration/**'
}

/**
 * Create the engine distribution
 */
task createWar( type: Copy ){
	outputs.upToDateWhen { false }
	dependsOn( build, zipJavadocs, shadowJar )

	// Move the runtime
	from( "build/distributions" ) {
		include "${project.name}-${version}.jar"
        into "engine/WEB-INF/lib"
    }
	// License it up
	from( "src/main/resources/META-INF/boxlang-servelet/license.txt" ) {
		include "license.txt"
	}
	// Resources
	from( "src/main/resources/boxlang-servlet" ) {
		include "web.xml"
		include "box.json"
		// Token Replacements Go Here
		if( project.branch == "development" ){
			filter{ line -> line.replaceAll( '@build.version@', project.version ) }
		} else {
			filter{ line -> line.replaceAll( '@build.version@', project.version + "+" + project.buildID ) }
		}
		into "engine/WEB-INF"
	}
	// Move to distro for packaging
	from( "build/engine/WEB-INF" ){
		include "box.json"
		into "distributions"
	}
	destinationDir = file( "build" )
	doLast {
		println "+ Engine has been created in the build/engine folder"
	}
}
task packageWar( type: War, dependsOn: createWar ){
	archiveFileName = "${project.name}-${version}.war"
	destinationDirectory = file( 'build/distributions' )
	from( 'build/engine' )
	doLast {
		println "+ War created in the distributions folder"
	}
}
task buildRuntime( type: Copy, dependsOn: packageWar ){
	finalizedBy( "zipEngine" )
	from( 'build/distributions' ) {
		include "${project.name}-${version}.war"
		include "box.json"
	}
	destinationDir = file( 'build/forgebox' )

	doLast {
		// Generate checksums for all zip and jar files in distributions folder
		file( "build/distributions" ).listFiles().each { file ->
			if ( file.name.endsWith( '.zip' ) || file.name.endsWith( '.jar' ) || file.name.endsWith( '.war' ) ) {
				generateChecksum( file, 'SHA-256' )
				generateChecksum( file, 'MD5' )
			}
		}

		file( "build/evergreen" ).mkdirs()
		if( branch == 'development' ){
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.war" ).toPath(), file( "build/evergreen/boxlang-servlet-snapshot.war" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.jar" ).toPath(), file( "build/evergreen/boxlang-servlet-snapshot.jar" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			// Checksums
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.jar.sha-256" ).toPath(), file( "build/evergreen/boxlang-servlet-snapshot.jar.sha-256" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.jar.md5" ).toPath(), file( "build/evergreen/boxlang-servlet-snapshot.jar.md5" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.war.sha-256" ).toPath(), file( "build/evergreen/boxlang-servlet-snapshot.war.sha-256" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.war.md5" ).toPath(), file( "build/evergreen/boxlang-servlet-snapshot.war.md5" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
		} else {
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.war" ).toPath(), file( "build/evergreen/boxlang-servlet-latest.war" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.jar" ).toPath(), file( "build/evergreen/boxlang-servlet-latest.jar" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			// Checksums
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.jar.sha-256" ).toPath(), file( "build/evergreen/boxlang-servlet-latest.jar.sha-256" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.jar.md5" ).toPath(), file( "build/evergreen/boxlang-servlet-latest.jar.md5" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.war.sha-256" ).toPath(), file( "build/evergreen/boxlang-servlet-latest.war.sha-256" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
			Files.copy( file( "build/distributions/boxlang-servlet-${version}.war.md5" ).toPath(), file( "build/evergreen/boxlang-servlet-latest.war.md5" ).toPath(), StandardCopyOption.REPLACE_EXISTING )
		}

		println "+ CommandBox Engine created in the build/forgebox folder, ready for publishing"
	}
}

/**
 * Integration test task that deploys the WAR to an embedded Jetty server
 * and verifies BoxLang template execution
 */
task testServer( type: Test ) {
	description = 'Runs integration tests by deploying the WAR to an embedded server'
	group = 'verification'

	// Only run the integration test
	useJUnitPlatform()
	include '**/integration/ServletIntegrationTest.class'

	// Make sure the WAR is built first
	dependsOn buildRuntime

	// Don't run this as part of the normal test task
	// It's specifically for integration testing the deployed WAR

	// Configure test logging to show essential output without verbose HTTP details
	testLogging {
		showStandardStreams = true
		events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
		exceptionFormat = 'short'
		showExceptions = true
		showCauses = true
		showStackTraces = false
	}

	doFirst {
		println "ðŸš€ Starting integration test with embedded server..."
		println "ðŸ“¦ Testing WAR deployment and BoxLang execution..."
	}

	doLast {
		println "âœ… Integration test completed successfully!"
	}

	// Increase test timeout for server startup
	systemProperty 'junit.jupiter.execution.timeout.default', '180s'
}

/**
 * Generate checksums for the given file using the specified algorithm

 * @param file The file to generate the checksum for
 * @param algorithm The algorithm to use (e.g., "SHA-256", "MD5")
 */
def generateChecksum( File file, String algorithm ) {
	def digest = java.security.MessageDigest.getInstance( algorithm )
	file.eachByte( 4096 ) { bytes, size ->
		digest.update( bytes, 0, size )
	}
	def checksum = digest.digest().collect { String.format( '%02x', it ) }.join()

	def checksumFile = new File( file.parent, "${file.name}.${algorithm.toLowerCase()}" )
	checksumFile.text = "${checksum}  ${file.name}\n"
}

/**
 * ZIp up a test server
 */
task zipEngine( type: Zip ) {
    archiveFileName = "boxlang-servlet-${version}.zip"
    destinationDirectory = file( "build/distributions" )
    from( "build/forgebox" ) {
        include "box.json"
        include "*.war"
    }
}

/**
 * TEMPORARY until we publish to maven
 * Task to download the latest jar from https://ortus-temp.s3.amazonaws.com/jericho/libs/boxlang-1.0.0.jar
 */
task downloadBoxLang( type: Download ) {
	// Create the destination directory
	doFirst {
		file( "src/test/resources/libs" ).mkdirs()
	}
	// Configure the URLs of the files to download
	src([
		"https://downloads.ortussolutions.com/ortussolutions/boxlang/${boxlangVersion}/boxlang-${boxlangVersion}.jar",
		"https://downloads.ortussolutions.com/ortussolutions/boxlang-runtimes/boxlang-web-support/${boxlangVersion}/boxlang-web-support-${boxlangVersion}.jar"
	])
	// Specify the destination directories for the downloaded files
	dest "src/test/resources/libs/"
    overwrite true
    onlyIfModified false
}
task repackBoxLangJar {
    dependsOn downloadBoxLang
    
    def jarFile = file("src/test/resources/libs/boxlang-web-support-${boxlangVersion}.jar")
    def tempDir = file("${buildDir}/tempJar")
    
    // Set up proper inputs/outputs for incremental builds
    inputs.file jarFile
    outputs.file jarFile
    
    doLast {
        if (!jarFile.exists()) {
            throw new GradleException("Source JAR file does not exist: ${jarFile}")
        }
        
        println "ðŸ”§ Repackaging ${jarFile.name} to remove conflicting GetPageContext classes..."
        
        // Clean and create temp directory
        if (tempDir.exists()) {
            tempDir.deleteDir()
        }
        tempDir.mkdirs()
        
        // Extract JAR contents excluding the problematic classes
        copy {
            from zipTree(jarFile)
            into tempDir
            exclude 'ortus/boxlang/web/bifs/GetPageContext.class'
            exclude 'ortus/boxlang/web/bifs/GetPageContext$PageContext.class'
        }
        
        // Delete the original JAR
        jarFile.delete()
        
        // Create new JAR from filtered contents
        ant.jar(destfile: jarFile.absolutePath) {
            fileset(dir: tempDir.absolutePath)
        }
        
        // Clean up temp directory
        tempDir.deleteDir()
        
        println "âœ… Successfully repackaged JAR without GetPageContext classes"
    }
}
downloadBoxLang.finalizedBy( repackBoxLangJar )




/**
 * Task to download the BoxLang Doc Generator
 * This is a temporary task until we publish it to maven
 */
task downloadDocGenerator( type: Download ) {
	// Create the destination directory
	doFirst {
		file( "src/test/resources/libs" ).mkdirs()
	}
    // Configure the URL of the file to download
    src "https://downloads.ortussolutions.com/ortussolutions/boxlang-runtimes/boxlang-doc-generator/1.0.0-snapshot/boxlang-doc-generator-1.0.0-snapshot.jar"
    // Specify the destination directory for the downloaded file
    dest "src/test/resources/libs/boxlang-doc-generator-1.0.0-snapshot.jar"
    overwrite true
    onlyIfModified false
}

/**
 * Generate/Scaffold technical documentation
 */
task generateTechnicalDocumentation( type: Javadoc ) {
	source = sourceSets.main.java
	dependsOn downloadDocGenerator, compileJava, serviceLoaderBuild
	// Uncomment and comment the above for local build testing of the doc generator
	// dependsOn compileJava, serviceLoaderBuild
	def classPaths = new ArrayList<File>( sourceSets.main.runtimeClasspath.getFiles() )
	classPaths.addAll( sourceSets.main.java.getFiles() )
	classPaths.addAll( files( 'src/test/resources/libs/boxlang-web-support-' + boxlangVersion + '.jar' ) )
	classPaths.addAll( files( 'src/test/resources/libs/boxlang-doc-generator-1.0.0-snapshot.jar' ) )
	options.classpath( classPaths )
	options.addBooleanOption( "Xdoclint:none", true )
	exclude '**/boxlang/parser/**'
	// exclude '**/boxlang/runtime/**'
	options.addBooleanOption( 'html5', true )
	options.doclet = "ortus.boxlang.tools.doclets.BoxLangDoclet"
    options.docletpath = classPaths
}

/**
 * Project Wide Helper function
 * This is not a task, but a reusable UDF
 */
project.ext.bumpVersion = {
	boolean major = false,
	boolean minor = false,
	boolean patch = false,
	boolean beta = false,
	boolean rc = false,
	property = "version" ->

	def propertiesFile = file( './gradle.properties' );
	def properties = new Properties();

	properties.load( propertiesFile.newDataInputStream() )
	def versionTarget = major ? 0 : minor ? 1 : beta ? 2 : 3

	def currentVersion = properties.getProperty( property )
	def versionParts = currentVersion.split( '\\.' )
	def newPathVersion = versionParts[ versionTarget ].toInteger() + 1
	def newVersion = '';

	if( patch ){
		newVersion = "${versionParts[ 0 ]}.${versionParts[ 1 ]}.${newPathVersion}"
	} else if( minor ){
		newVersion = "${versionParts[ 0 ]}.${newPathVersion}.${versionParts[ 2 ]}"
	} else if( major ){
		newVersion = "${newPathVersion}.${versionParts[ 1 ]}.${versionParts[ 2 ]}"
	} else if( beta ){
		// Get's the -betaX version.
		def betaString = currentVersion.split( '-' )[ 1 ]
		// Now we get the beta number
		def betaNumber = betaString.split( 'beta' )[ 1 ].toInteger() + 1
		newVersion = currentVersion.split( '-' )[ 0 ] + "-beta${betaNumber}"
	} else if( rc ){
		newVersion = "${versionParts[ 0 ]}.${versionParts[ 1 ]}.${versionParts[ 2 ]}.${newPathVersion}"
	}

	properties.setProperty( property, newVersion )
	properties.store( propertiesFile.newWriter(), null )

	println "Bumped version from ${currentVersion} to ${newVersion}"
}

/**
 * Bump the major version number
 */
task bumpMajorVersion {
	doLast{
		bumpVersion( true, false, false, false, false, "version" )
		bumpVersion( true, false, false, false, false, "boxlangVersion" )
	}
}

/**
 * Bump the minor version number
 */
task bumpMinorVersion {
	doLast{
		bumpVersion( false, true, false, false, false, "version" )
		bumpVersion( false, true, false, false, false, "boxlangVersion" )
	}
}

/**
 * Bump the patch version number
 */
task bumpPatchVersion {
	doLast{
		bumpVersion( false, false, true, false, false, "version" )
		bumpVersion( false, false, true, false, false, "boxlangVersion" )
	}
}

/**
 * Bump the beta version number
 */
task bumpBetaVersion {
	doLast{
		bumpVersion( false, false, false, true, false, "version" )
		bumpVersion( false, false, false, true, false, "boxlangVersion" )
	}
}

/**
 * Bump the RC version number
 */
task bumpRCVersion {
	doLast{
		bumpVersion( false, false, false, false, true, "version" )
		bumpVersion( false, false, false, false, true, "boxlangVersion" )
	}
}
